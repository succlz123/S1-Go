def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "org.succlz123.s1go.app"
        minSdkVersion 21
        targetSdkVersion 28

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        versionCode 21
        versionName "1.1.3"

        buildConfigField 'String', 'AUTH', '\"auth\"'
        buildConfigField 'String', 'SALT_KEY', '\"saltkey\"'
        buildConfigField 'String', 'FORM_HASH', '\"formhash\"'

        buildConfigField 'String', 'POST_REPLY_SUCCEED', '\"post_reply_succeed\"'
        buildConfigField 'String', 'POST_NEW_THREAD_SUCCEED', '\"post_newthread_succeed\"'
        buildConfigField 'String', 'LOGIN_MOBILE_SUCCEED', '\"location_login_succeed_mobile\"'
        buildConfigField 'String', 'LOGIN_SUCCEED', '\"login_succeed\"'
        buildConfigField 'String', 'LOGIN_FAILED', '\"login_invalid\"'

        buildConfigField 'String', 'PHONE_INFO', '\"\\n \\n \\n—— 发送自 [url=https://play.google.com/store/apps/details?id=org.succlz123.s1go.app] Stage1st Go \" + android.os.Build.MODEL + \" Android \" + android.os.Build.VERSION.RELEASE + "[/url]"'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }

    buildTypes {
        debug {
            zipAlignEnabled false
            minifyEnabled false
            debuggable true
            jniDebuggable true
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "com.android.support:support-v4:28.0.0"
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:recyclerview-v7:28.0.0"
    implementation "com.android.support:design:28.0.0"

    implementation "com.squareup.retrofit2:retrofit:2.2.0"
    implementation "com.squareup.retrofit2:converter-gson:2.2.0"
    implementation "com.squareup.retrofit2:adapter-rxjava:2.2.0"
    implementation "io.reactivex:rxandroid:1.2.1"
    implementation "io.reactivex:rxjava:1.2.3"

    implementation("com.facebook.fresco:fresco:1.2.0") {
        exclude group: 'com.nineoldandroids', module: 'library'
        exclude group: 'com.android.support'
    }

    implementation "com.tencent.bugly:crashreport:2.6.6"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.5.4"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.5.4"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.5.4"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation "org.qiibeta.views:BitmapView:1.0.5"
}
